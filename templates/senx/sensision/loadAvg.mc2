0 MACROTTL
{
  'name' '@senx/sensision/loadAvg'
  'desc' 
  <'
 Get the average load data from the Sensision.
  '>
  'sig' [ [ [ 'token:STRING' 'period:STRING' ] [ 'result:LIST<GTS>'  ] ] ] // Signature
  'params' {
    'token' 'The sensision read token'
    'period' 'The period to query (h: hour, d: day, w: week, m: month, y: year)'
    'result' 'List of GTS to return'
  }
  'examples' [
    <'

    '>
  ]
} 'info' STORE

<%
  !$info INFO
  SAVE 'context' STORE
  <%
    @senx/sensision/utils/periodMappings
    [ 'token' 'period' ] STORE

    <%
        [
            $token '~linux.proc.stat.userhz.(user|nice|system|idle|iowait)'
            { 'cpu' 'cpu' }
            NOW $periods $period GET
        ] FETCH
        [ SWAP bucketizer.mean NOW 0 $buckets $period GET ] BUCKETIZE 'gts' STORE
        [ $gts [] '~linux.proc.stat.userhz.(user|nice|system|idle|iowait)' filter.byclass ] FILTER [ SWAP [] reducer.sum ] REDUCE 0 GET 'cpuGTS' STORE
        [ $gts [] 'linux.proc.stat.userhz.idle' filter.byclass ] FILTER 0 GET 'idleGTS' STORE
        [ $gts [] '~linux.proc.stat.userhz.(iowait|idle)' filter.byclass ] FILTER [ SWAP [] reducer.sum ] REDUCE 0 GET 'iowaitGTS' STORE
        [ $gts [] '~linux.proc.stat.userhz.(system|idle)' filter.byclass ] FILTER [ SWAP [] reducer.sum ] REDUCE 0 GET 'systemGTS' STORE
        [
        [ [ $cpuGTS $idleGTS ] [] <%
            'd' STORE
            $d 7 GET 0 GET 'cpu' STORE
            $d 7 GET 1 GET 'idle' STORE
            1000.0 $cpu $idle - * $cpu 5  + / 10 / 'v' STORE
            [ $d 0 GET NaN NaN NaN $v ]
        %> MACROREDUCER ] REDUCE 0 GET 'CPU' RENAME

        [ [ $iowaitGTS $idleGTS ] [] <%
            'd' STORE
            $d 7 GET 0 GET 'cpu' STORE
            $d 7 GET 1 GET 'idle' STORE
            1000.0 $cpu $idle - * $cpu 5  + / 10 / 'v' STORE
            [ $d 0 GET NaN NaN NaN $v ]
        %> MACROREDUCER ] REDUCE 0 GET 'Disk IO' RENAME

        [ [ $systemGTS $idleGTS ] [] <%
            'd' STORE
            $d 7 GET 0 GET 'cpu' STORE
            $d 7 GET 1 GET 'idle' STORE
            1000.0 $cpu $idle - * $cpu 5  + / 10 / 'v' STORE
            [ $d 0 GET NaN NaN NaN $v ]
        %> MACROREDUCER ] REDUCE 0 GET 'System' RENAME
        ]
    %> <% [] %> <% %> TRY 
  %>
  <% // catch any exception
    RETHROW
  %>
  <% // finally, restore the context
    $context RESTORE
  %> TRY
%>
'macro' STORE

// Unit tests

$macro