0 MACROTTL
{
  'name' '@senx/sensision/ingress'
  'desc' 
  <'
 Get the ingress data from the Sensision.
  '>
  'sig' [ [ [ 'token:STRING' 'period:STRING' ] [ 'result:LIST<GTS>'  ] ] ] // Signature
  'params' {
    'token' 'The sensision read token'
    'period' 'The period to query (h: hour, d: day, w: week, m: month, y: year)'
    'result' 'List of GTS to return'
  }
  'examples' [
    <'

    '>
  ]
} 'info' STORE

<%
  !$info INFO
  SAVE 'context' STORE
  <%
    @senx/sensision/utils/periodMappings
    [ 'token' 'period' ] STORE
    [
        $token
        'warp.standalone.update.datapoints.raw'
        {} NOW $periods $period GET
    ] FETCH false RESETS
    [ SWAP bucketizer.mean NOW 0 $buckets  $period GET ] BUCKETIZE
    [ SWAP mapper.delta 1 0 0 ] MAP
    [ NaN NaN NaN 0 ] FILLVALUE SORT
    [ SWAP [] reducer.sum ] REDUCE
      
  %>
  <% // catch any exception
    RETHROW
  %>
  <% // finally, restore the context
    $context RESTORE
  %> TRY
%>
'macro' STORE

// Unit tests

$macro